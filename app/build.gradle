plugins {
    id 'com.android.application'
}

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file('keystore.properties')))

android {
    signingConfigs {
        sign_app {
            storeFile file(rootProject.file(keystoreProperties['storeFile']))
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdkVersion 32

    def versioncodeFile = file(rootProject.file('update.properties'))
    Properties versionProperties = new Properties()
    if (!versioncodeFile.exists()) {
        versionProperties['VER'] = "3.14 (0)"
        versionProperties.store(versioncodeFile.newWriter(), null)
    }
    versionProperties.load(new FileInputStream(versioncodeFile))

    def code = versionProperties['VER'].substring(versionProperties['VER'].indexOf("(") + 1, versionProperties['VER'].indexOf(")")) as Integer

    for (String item : gradle.startParameter.getTaskNames()) {
        if (item.contains("assembleRelease")) {
            code++
            break
        }
    }

    if (code == 0) //if update.properties did not exist and the Build Variant is debug
        code = 1

    versionProperties['VER'] = "3.14 (${code})" as String
    versionProperties.store(versioncodeFile.newWriter(), null)

    defaultConfig {
        applicationId 'com.taffo.lockscreenear'
        minSdkVersion 24
        targetSdkVersion 32
        versionCode code
        versionName "3.14 (${code})"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign_app
            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "LockscreenEar.apk"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    useLibrary 'org.apache.http.legacy'
    namespace 'com.taffo.lockscreenear'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'org.jetbrains:annotations:23.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    }
}
